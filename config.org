#+TITLE: Emacs Config
#+AUTHOR: Adwait Adhikari
#+PROPERTY: header-args :tangle init.el
#+STARTUP: showeverything
#+OPTIONS: toc:2 ^:{}
#+auto_tangle: t

* Table of Contents :TOC:
- [[#important-initializations][Important Initializations]]
  - [[#early-init][Early Init]]
  - [[#loading-scripts--custom-file][Loading Scripts & Custom File]]
  - [[#evil-mode][Evil Mode]]
  - [[#general-keybindings][General Keybindings]]
  - [[#icons][Icons]]
  - [[#modeline][Modeline]]
  - [[#sane-defaults][Sane Defaults]]
  - [[#session-preservation][Session Preservation]]
  - [[#theme][Theme]]
  - [[#which-key][Which Key]]
- [[#applications][Applications]]
  - [[#avy][Avy]]
  - [[#browse-url][Browse URL]]
  - [[#buffer-alist][Buffer Alist]]
  - [[#buffer-commands][Buffer Commands]]
  - [[#gptel][Gptel]]
  - [[#expand-region][Expand Region]]
  - [[#elfeed][Elfeed]]
  - [[#eww][Eww]]
  - [[#mu4e][Mu4e]]
  - [[#pdf-tools][PDF Tools]]
  - [[#uniline][Uniline]]
- [[#buffer-management][Buffer Management]]
  - [[#ibuffer][Ibuffer]]
  - [[#perspective][Perspective]]
- [[#development][Development]]
  - [[#completions][Completions]]
  - [[#formatters][Formatters]]
  - [[#flymake][Flymake]]
  - [[#language-servers][Language Servers]]
  - [[#language-configs][Language Configs]]
  - [[#ligatures][Ligatures]]
  - [[#rainbow-delimiters][Rainbow Delimiters]]
  - [[#projectile][Projectile]]
  - [[#sops][SOPS]]
  - [[#treesitter][Treesitter]]
- [[#dired][Dired]]
  - [[#diredfl][Diredfl]]
  - [[#dired-open][Dired Open]]
  - [[#dired-rainbow][Dired Rainbow]]
  - [[#peep-dired][Peep Dired]]
- [[#fonts][Fonts]]
  - [[#setting-font-faces][Setting Font Faces]]
  - [[#zooming-inout][Zooming In/Out]]
- [[#git][Git]]
  - [[#diff-hl][Diff HL]]
  - [[#magit][Magit]]
- [[#helpful][Helpful]]
- [[#minibuffer-completion][Minibuffer Completion]]
  - [[#consult][Consult]]
  - [[#embark][Embark]]
  - [[#extended-settings][Extended Settings]]
  - [[#icons-1][Icons]]
  - [[#marginalia][Marginalia]]
  - [[#oderless][Oderless]]
  - [[#vertico][Vertico]]
- [[#org-mode][Org Mode]]
  - [[#better-fonts][Better Fonts]]
  - [[#org-roam][Org Roam]]
  - [[#prettify-symbols][Prettify Symbols]]
  - [[#settings][Settings]]
  - [[#small-utilities][Small Utilities]]
  - [[#svg-tags][SVG Tags]]
  - [[#visual-fill-column][Visual Fill Column]]
- [[#shells--terminals][Shells & Terminals]]
  - [[#eshell][Eshell]]
  - [[#vterm][Vterm]]
- [[#undo][Undo]]
  - [[#undo-fu][Undo Fu]]
  - [[#vundo][Vundo]]

* Important Initializations
** Early Init
Emacs is an Elisp interpreter, and when running programs or packages, it can occasionally experience pauses due to garbage collection. By increasing the garbage collection threshold, we reduce these pauses during heavy operations, leading to smoother performance. The default is 800 kilobytes. Measured in bytes.

~most-positive-fixnum~ is *DANGEROUS AS A PERMANENT VALUE*. See the ~elpaca-after-init-hook~ for what I actually use.
#+begin_src emacs-lisp :tangle early-init.el
  ;;; early-init.el --- Early Init -*- no-byte-compile: t; lexical-binding: t; -*-

  (setq gc-cons-threshold most-positive-fixnum
        gc-cons-percentage 0.5)

  (add-hook 'elpaca-after-init-hook
            (lambda ()
              (setq gc-cons-threshold (* 50 1024 1024 1024)
                    gc-cons-percentage 0.1)))
#+end_src

Performance optimizations and other tweaks taken from [[https://github.com/jamescherti/minimal-emacs.d/blob/main/early-init.el][minimal-emacs.d]]
#+begin_src emacs-lisp :tangle early-init.el
  ;;; Misc

  (set-language-environment "UTF-8")

  ;; Set-language-environment sets default-input-method, which is unwanted.
  (setq default-input-method nil)

  ;;; Performance

  ;; Prefer loading newer compiled files
  (setq load-prefer-newer t)

  ;; Font compacting can be very resource-intensive, especially when rendering
  ;; icon fonts on Windows. This will increase memory usage.
  (setq inhibit-compacting-font-caches t)

  (unless (daemonp)
    (let ((old-value (default-toplevel-value 'file-name-handler-alist)))
      (set-default-toplevel-value
       'file-name-handler-alist
       ;; Determine the state of bundled libraries using calc-loaddefs.el.
       ;; If compressed, retain the gzip handler in `file-name-handler-alist`.
       ;; If compiled or neither, omit the gzip handler during startup for
       ;; improved startup and package load time.
       (if (eval-when-compile
             (locate-file-internal "calc-loaddefs.el" load-path))
           nil
         (list (rassq 'jka-compr-handler old-value))))
      ;; Ensure the new value persists through any current let-binding.
      (set-default-toplevel-value 'file-name-handler-alist
                                  file-name-handler-alist)
      ;; Remember the old value to reset it as needed.
      (add-hook 'emacs-startup-hook
                (lambda ()
                  (set-default-toplevel-value
                   'file-name-handler-alist
                   ;; Merge instead of overwrite to preserve any changes made
                   ;; since startup.
                   (delete-dups (append file-name-handler-alist old-value))))
                101))

    (unless noninteractive
          ;; Suppress redisplay and redraw during startup to avoid delays and
          ;; prevent flashing an unstyled Emacs frame.
          ;; (setq-default inhibit-redisplay t) ; Can cause artifacts
          (setq-default inhibit-message t)

          ;; Reset the above variables to prevent Emacs from appearing frozen or
          ;; visually corrupted after startup or if a startup error occurs.
          (defun minimal-emacs--reset-inhibited-vars-h ()
            ;; (setq-default inhibit-redisplay nil) ; Can cause artifacts
            (setq-default inhibit-message nil)
            (remove-hook 'post-command-hook #'minimal-emacs--reset-inhibited-vars-h))

          (add-hook 'post-command-hook
                    #'minimal-emacs--reset-inhibited-vars-h -100))

        (dolist (buf (buffer-list))
          (with-current-buffer buf
            (setq mode-line-format nil)))

        (put 'mode-line-format 'initial-value
             (default-toplevel-value 'mode-line-format))
        (setq-default mode-line-format nil)

        (defun minimal-emacs--startup-load-user-init-file (fn &rest args)
          "Advice for startup--load-user-init-file to reset mode-line-format."
          (unwind-protect
              (progn
                ;; Start up as normal
                (apply fn args))
            ;; If we don't undo inhibit-{message, redisplay} and there's an
            ;; error, we'll see nothing but a blank Emacs frame.
            (setq-default inhibit-message nil)
            (unless (default-toplevel-value 'mode-line-format)
              (setq-default mode-line-format
                            (get 'mode-line-format 'initial-value)))))

        (advice-add 'startup--load-user-init-file :around
                    #'minimal-emacs--startup-load-user-init-file))

      ;; Without this, Emacs will try to resize itself to a specific column size
      (setq frame-inhibit-implied-resize t)

      ;; A second, case-insensitive pass over `auto-mode-alist' is time wasted.
      ;; No second pass of case-insensitive search over auto-mode-alist.
      (setq auto-mode-case-fold nil)

      ;; Reduce *Message* noise at startup. An empty scratch buffer (or the
      ;; dashboard) is more than enough, and faster to display.
      (setq inhibit-startup-screen t
            inhibit-startup-echo-area-message user-login-name)
      (setq initial-buffer-choice nil
            inhibit-startup-buffer-menu t
            inhibit-x-resources t)

      ;; Disable bidirectional text scanning for a modest performance boost.
      (setq-default bidi-display-reordering 'left-to-right
                    bidi-paragraph-direction 'left-to-right)

      ;; Give up some bidirectional functionality for slightly faster re-display.
      (setq bidi-inhibit-bpa t)

      ;; Remove "For information about GNU Emacs..." message at startup
      (advice-add #'display-startup-echo-area-message :override #'ignore)

      ;; Suppress the vanilla startup screen completely. We've disabled it with
      ;; `inhibit-startup-screen', but it would still initialize anyway.
      (advice-add #'display-startup-screen :override #'ignore)

      ;; Shave seconds off startup time by starting the scratch buffer in
      ;; `fundamental-mode'
      (setq initial-major-mode 'fundamental-mode
            initial-scratch-message nil)

      ;; Unset command line options irrelevant to the current OS. These options
      ;; are still processed by `command-line-1` but have no effect.
      (unless (eq system-type 'darwin)
        (setq command-line-ns-option-alist nil))
      (unless (memq initial-window-system '(x pgtk))
        (setq command-line-x-option-alist nil))

  ;;; Native compilation and Byte compilation

  (if (and (featurep 'native-compile)
           (fboundp 'native-comp-available-p)
           (native-comp-available-p))
      ;; Activate `native-compile'
      (setq native-comp-jit-compilation t
            package-native-compile t)
    ;; Deactivate the `native-compile' feature if it is not available
    (setq features (delq 'native-compile features)))

  ;; Disable startup screens and messages
  (setq inhibit-splash-screen t)

  ;; I intentionally avoid calling `menu-bar-mode', `tool-bar-mode', and
  ;; `scroll-bar-mode' because manipulating frame parameters can trigger or queue
  ;; a superfluous and potentially expensive frame redraw at startup, depending
  ;; on the window system. The variables must also be set to `nil' so users don't
  ;; have to call the functions twice to re-enable them.
  (push '(menu-bar-lines . 0) default-frame-alist)
  (unless (memq window-system '(mac ns))
    (setq menu-bar-mode nil))

  (unless (daemonp)
    (unless noninteractive
      (when (fboundp 'tool-bar-setup)
        ;; Temporarily override the tool-bar-setup function to prevent it from
        ;; running during the initial stages of startup
        (advice-add #'tool-bar-setup :override #'ignore)
        (define-advice startup--load-user-init-file
            (:after (&rest _) minimal-emacs-setup-toolbar)
          (advice-remove #'tool-bar-setup #'ignore)
          (when tool-bar-mode
            (tool-bar-setup))))))

  (push '(tool-bar-lines . 0) default-frame-alist)
  (setq tool-bar-mode nil)

  (push '(vertical-scroll-bars) default-frame-alist)
  (push '(horizontal-scroll-bars) default-frame-alist)
  (setq scroll-bar-mode nil)
  (when (fboundp 'horizontal-scroll-bar-mode)
    (horizontal-scroll-bar-mode -1))

  (when (bound-and-true-p tooltip-mode)
    (tooltip-mode -1))

  ;; Disable GUIs because they are inconsistent across systems, desktop
  ;; environments, and themes, and they don't match the look of Emacs.
  (setq use-file-dialog nil)
  (setq use-dialog-box nil)
#+end_src

The early init file contains disabling ~package.el~, Emacs' default package manager, as we are about to use [[https://github.com/progfolio/elpaca][elpaca]] for package management.
#+begin_src emacs-lisp :tangle early-init.el

  (setq package-enable-at-startup nil)

#+end_src

We are also setting a const variable to locate our custom emacs directories.
#+begin_src emacs-lisp
  ;;; init.el --- Init -*- no-byte-compile: t; lexical-binding: t; -*-

  (defconst nox/emacs-directory (concat (getenv "HOME") "/.config/aurora-emacs/" ))
  (defconst nox/notes-directory (concat (getenv "HOME") "/Documents/Zettels/"))

#+end_src

** Loading Scripts & Custom File
Displaying the time it took for emacs to start along with total number of garbage collections at startup. Throught this file we also replace ~emacs-startup-hook~ and ~after-init-hook~ with =elpaca-after-init-hook= since we are using [[https://github.com/progfolio/elpaca][elpaca]] for package management.
#+begin_src emacs-lisp
  ;;; init.el --- file for init -*- no-byte-compile: t; lexical-binding: t; -*-

  (defun nox/display-startup-time ()
    "Display the emacs startup time"
    (interactive)
    (message "Emacs loaded in %s with %d garbage collections."
             (format "%.2f seconds"
                     (float-time
                      (time-subtract after-init-time before-init-time)))
             gcs-done))

#+end_src

We are also setting a const variable to locate our custom emacs directories.
#+begin_src emacs-lisp

  (defconst nox/emacs-directory (concat (getenv "HOME") "/.config/minimal-emacs/" ))
  (defconst nox/notes-directory (concat (getenv "HOME") "/Documents/Zettels/"))

#+end_src

Here we specify the ~scripts~ directory to contains other elisp files that we are about to load. We also specify a custom ~custom-file~ so that emacs doesn't messup out ~init.el~.
#+begin_src emacs-lisp

  (add-to-list 'load-path (concat nox/emacs-directory "scripts/"))

  ;; setting the core date by searching from hydra for nixos
  (setq elpaca-core-date '(20241224))
  (require 'elpaca-setup)   ;; Elpaca Package Manager
  (require 'on)             ;; Doom Style Hooks
  (require 'minimal)        ;; emacs config from minimal-emacs.d
  (require 'link-converter) ;; md <=> converter

  ;; Specify the custom file path
  (setq custom-file (concat nox/emacs-directory "custom-vars.el"))

  ;; Load the custom file quietly
  (add-hook 'elpaca-after-init-hook
            (lambda () (load custom-file 'noerror 'nomessage)))

#+end_src

** Evil Mode
[[https://github.com/emacs-evil/evil][Evil mode]], emulating vim keybinds inside emacs. *Truely Evil!* Here we also change the undo system to utilize [[#undo-fu][undo fu]].
#+begin_src emacs-lisp

  (use-package benchmark-init
    :ensure t
    :config
    (add-hook 'after-init-hook 'benchmark-init/deactivate))

  (use-package evil
    :custom
    (evil-want-integration t)
    (evil-want-C-u-scroll t)
    (evil-want-keybinding nil)
    (evil-undo-system 'undo-fu)
    (evil-vsplit-window-right t)
    (evil-vsplit-window-below t)
    :config
    (evil-mode)

#+end_src

Motion state is an Evil-specific thing, intended for modes where you don't edit text, but still want Vim-style movement available, with all other keys of that mode passing through. Help buffers are an example of such a case.
#+begin_src emacs-lisp

  (evil-define-key 'motion 'global
    (kbd "C-w u") 'winner-undo
    "j" 'evil-next-visual-line
    "k" 'evil-previous-visual-line)

#+end_src

Here we are setting bindings for normal mode globally.
#+begin_src emacs-lisp

  (evil-define-key 'normal 'global
    (kbd "C-S-v") 'cua-set-mark
    (kbd "C-o") 'casual-editkit-main-tmenu
    "s" 'evil-avy-goto-char-timer)

#+end_src

These are some niceties that I want cause I have been using them for a long time.
#+begin_src emacs-lisp

  (evil-define-key '(normal visual) 'global
    "P" 'consult-yank-from-kill-ring
    "H" 'evil-first-non-blank
    "L" 'evil-end-of-line)

#+end_src

We can also set bindings specific to a mode. Here we are setting bindings specific to normal state in org-mode.
#+begin_src emacs-lisp

  (evil-define-key 'normal org-mode-map
    "J" 'org-shiftright
    "K" 'org-shiftleft)

#+end_src

With the same vein of thought we have also added a binding to elfeed search mode to easily view an entry.
#+begin_src emacs-lisp

  (evil-define-key 'normal elfeed-search-mode-map
    "l" 'elfeed-search-show-entry))

#+end_src

We are also installing evil-collection so that we have sane evil bindings in almost all modes.
#+begin_src emacs-lisp

  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init)
    :custom
    (evil-collection-want-find-usages-bindings t))

#+end_src

** General Keybindings
[[https://github.com/noctuid/general.el][General]] helps set keybinds for emacs. The following contains all the rest of keybinds that don't directly relate to evil binds. Here we also set =SPC= as the prefix key and =C-SPC= as global prefix. Global prefix basically means prefix key but in every single state.
#+begin_src emacs-lisp

  ;; Make ESC quit prompts
  (global-set-key (kbd "<escape>") 'keyboard-escape-quit)

  (use-package general
    :config
    (general-evil-setup)
    (general-create-definer nox/leader-keys
      :states '(normal insert visual emacs)
      :keymaps 'override
      :prefix "SPC"
      :global-prefix "C-SPC")

#+end_src

Bindings specific to application inside emacs.
#+begin_src emacs-lisp

  (nox/leader-keys
    "a" '(:ignore t :wk "[A]pplications")
    "a e" '(elfeed :wk "[E]lfeed")
    "a g" '(gptel :wk "[G]ptel")
    "a m" '(mu4e :wk "[M]ail"))

#+end_src

Bindings specific to buffer management go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "b" '(:ignore t :wk "[B]uffer")
    "b b" '(consult-buffer :wk "[B]uffer Switch")
    "b i" '(persp-ibuffer :wk "[I]buffer")
    "b k" '(kill-current-buffer :wk "[K]ill Buffer")
    "b n" '(next-buffer :wk "[N]ext Buffer")
    "b p" '(previous-buffer :wk "[P]revious Buffer")
    "b s" '(hydra-buffer-switch/body :wk "[S]witch Buffer")
    "b r" '(revert-buffer :wk "[R]eload Buffer"))

#+end_src

Bindings specific to dired go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "d" '(:ignore t :wk "[D]ired")
    "d ." '(dired-omit-mode :wk "Toggle dot files")
    "d d" '(dired-jump :wk "[D]ired")
    "d h" '(dired-hide-details-mode :wk "[D]ired")
    "d p" '(peep-dired :wk "[P]eep Dired"))

#+end_src

Bindings specific to evaluating elisp go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "e" '(:ignore t :wk "[E]val")
    "e b" '(eval-buffer :wk "[B]uffer Eval")
    "e d" '(eval-defun :wk "[D]efun Eval")
    "e e" '(eval-expression :wk "[E]xpression Eval")
    "e l" '(eval-last-sexp :wk "[E]xpression Before Eval")
    "e r" '(eval-region :wk "[R]egion Eval"))

#+end_src

Bindings which relates to files go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "f" '(:ignore t :wk "[F]ile")
    "f c" `((lambda () (interactive) (find-file ,(concat nox/emacs-directory "config.org")))
            :wk "Edit emacs config")
    "f s" '(save-buffer :wk "[S]ave Buffer")
    "f u" '(sudo-edit-find-file :wk "S[U]do Find File")
    "f U" '(sudo-edit :wk "S[U]do Edit File"))

#+end_src

Bindings specifig to working with git go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "g" '(:ignore t :wk "[G]it")
    "g g" '(magit-status :wk "[G]it Status")
    "g s" '(diff-hl-stage-dwim :wk "[G]it Stage Hunk"))

#+end_src

Bindings specic to org things go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "o" '(:ignore t :wk "[O]rg")
    "o a" '(org-agenda :wk "[A]genda")
    "o e" '(org-edit-src-code :wk "[E]dit Src Code")
    "o x" '(org-toggle-checkbox :wk "[C]heckbox")
    "o l" '(org-store-link :wk "[L]ink Store")
    "o b" '(:ignore t :wk "[B]abel")
    "o b t" '(org-babel-tangle :wk "[T]angle")
    "o b d" '(org-babel-demarcate-block :wk "[D]emarcate Block"))
#+end_src

Bindings specific to org properties.
#+begin_src emacs-lisp

  (nox/leader-keys
    "o p" '(:ignore t :wk "[P]roperties")
    "o p s" '(org-schedule :wk "[S]chedule")
    "o p d" '(org-deadline :wk "[D]eadline")
    "o p e" '(org-set-effort :wk "[E]ffort")
    "o p t" '(nox/org-toggle-properties :wk "[T]oggle Properties")
    "o p p" '(org-set-effort :wk "[P]roperty"))
#+end_src

Bindings even more specific to [[#org-roam][org roam]].
#+begin_src emacs-lisp

  (nox/leader-keys
    "o i" '(org-roam-node-insert :wk "[I]nsert Link")
    "o c" '((lambda () (interactive) (org-roam-capture nil "d")) :wk "[C]apture")
    "o C" '(org-roam-capture :wk "[C]apture with Templates")
    "o f" '(org-roam-node-find :wk "[F]ind Node")
    "o t" '(nox/org-roam-capture-tasks :wk "[T]ask Capture")
    "o d" '((lambda () (interactive) (org-roam-dailies-capture-today nil "d")) :wk "[D]aily Capture")
    "o D" '(org-roam-dailies-capture-today :wk "[D]aily Open"))

#+end_src

Bindings related to quitting utilities in emacs go here. /Hardly ever used!/ But seriously, these come in handy especially the restore perspectives for [[#perspective][perspective]].
#+begin_src emacs-lisp

  (nox/leader-keys
    "q" '(:ignore t :wk "[Q]uit")
    "q f" '(delete-frame :wk "[F]rame delete")
    "q r" '(nox/restore-perspectives :wk "[R]estore perspectives")
    "q K" '(kill-emacs :wk "[K]ill emacs"))

#+end_src

Bindings related to performing search of any kind go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "s" '(:ignore t :wk "[S]earch")
    "s g" '(consult-ripgrep :wk "[G]rep in dir")
    "s i" '(consult-imenu :wk "[I]menu")
    "s f" '(consult-fd :wk "[F]d Consult")
    "s r" '(consult-recent-file :wk "[R]recent File")
    "s m" '(bookmark-jump :wk "[M]arks")
    "s c" '(consult-mode-command :wk "[C]ommands for mode"))

#+end_src

Bindings specific to toggling things go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "t" '(:ignore t :wk "[T]oggle")
    "t e" '(eshell-toggle :wk "[E]shell")
    "t l" '(elpaca-log :wk "[L]og Elpaca")
    "t t" '(modus-themes-toggle :wk "[T]oggle Theme")
    "t c" '(visual-fill-column-mode :wk "[C]olumn Fill Mode")
    "t v" '(vterm-toggle :wk "[V]term")
    "t n" '(display-line-numbers-mode :wk "[N]umbered Lines")
    "t s" '(hydra-text-scale/body :wk "[S]cale Text"))

#+end_src

Anything that doesn't go in the above categories go here.
#+begin_src emacs-lisp

  (nox/leader-keys
    "." '(find-file :wk "Find File")
  ))

#+end_src

** Icons
#+begin_src emacs-lisp

  (use-package all-the-icons)
  (use-package nerd-icons :defer 2)

#+end_src

** Modeline
[[https://github.com/seagle0128/doom-modeline][Doom Modeline]], the best modeline! Here I have removed some elements from the modeline such as encoding format and percentage position.
#+begin_src emacs-lisp

  (use-package doom-modeline
    :hook
    (on-init-ui . doom-modeline-mode)
    :config
    (setq find-file-visit-truename t)
    (setq doom-modeline-icon t)
    (setq doom-modeline-buffer-encoding nil)
    (setq doom-modeline-percent-position nil)
    (setq doom-modeline-height 36))

#+end_src

In some context you don't want the modeline visible, this is where it comes in handy. Take the start page as an example.
#+begin_src emacs-lisp

  (use-package hide-mode-line :commands hide-mode-line-mode)

#+end_src

** Sane Defaults
Everything that you would tipically expect when coming form other IDEs. This also includes some niceties that emacs provides but not enabled by default.
#+begin_src emacs-lisp

  (use-package emacs
    :ensure nil
    :hook
    (prog-mode . display-line-numbers-mode)
    :init
    (electric-indent-mode -1)    ;; Disable weird emacs indenting
    (indent-tabs-mode -1)        ;; Disable the use of tabs for indentation
    (line-number-mode -1)        ;; Disable line number from showing in modline
    (xterm-mouse-mode 1)         ;; Enable mouse support in terminal mode.
    (file-name-shadow-mode 1)    ;; Enable shadowing of filenames for clarity.
    (electric-pair-mode 1)       ;; Enable pair parens
    (display-battery-mode 1)     ;; Enable displaying battery info in modline
    (winner-mode 1)              ;; Easily undo window configuration changes.
    :custom
    (delete-selection-mode 1)             ;; Replacing selected text with typed text.
    (global-visual-line-mode 1)           ;; Better text wrapping
    (display-line-numbers-type 'relative) ;; Use relative line numbering
    (history-length 25)                   ;; Set the length of the command history.
    (ispell-dictionary "en_US")           ;; Default dictionary for spell checking.
    (pixel-scroll-precision-mode t)       ;; Enable precise pixel scrolling.
    (ring-bell-function 'ignore)          ;; Disable the audible bell.
    (tab-width 4)                         ;; Set the tab width to 4 spaces.
    (use-dialog-box nil)                  ;; Disable dialog boxes
    (warning-minimum-level :error)        ;; Set the minimum level of warnings.
    (show-paren-context-when-offscreen t) ;; Show context of parens when offscreen
    (pixel-scroll-precision-use-momentum nil) ;; Disable momentum scrolling

    ;; Don't jump the cursor around when scrolling
    (scroll-conservatively 101)
    ;; TAB key complete, instead of just indenting.
    (tab-always-indent 'complete)
    ;; Use advanced font locking for Treesit mode.
    (treesit-font-lock-level 4)
    ;; Offer to delete any autosave file when killing a buffer.
    (kill-buffer-delete-auto-save-files t)
    ;; Prevent automatic window splitting if the window width exceeds 300 pixels.
    (split-width-threshold 300)
    :config
    (add-hook 'before-save-hook 'delete-trailing-whitespace)
    (setq-default indent-tabs-mode nil))

  (use-package woman
    :ensure nil
    :hook
    (woman-mode . visual-fill-column-mode)
    :custom
    (woman-fill-frame t))

 #+end_src

** Session Preservation
*** Auto Save
#+begin_src emacs-lisp

  (setq auto-save-default t     ; auto-save every buffer that visits a file
        auto-save-timeout 20    ; number of seconds idle time before auto-save
        auto-save-interval 200  ; number of keystrokes between auto-saves

        ;; Do not auto-disable auto-save after deleting large chunks of text
        auto-save-include-big-deletions t)

  (setq auto-save-list-file-prefix
        (expand-file-name "autosave/" user-emacs-directory))
  (setq tramp-auto-save-directory
        (expand-file-name "tramp-autosave/" user-emacs-directory))

#+end_src

*** Auto revert
Auto-revert in Emacs is a feature that automatically updates the contents of a buffer to reflect changes made to the underlying file on disk.
#+begin_src emacs-lisp

  (global-auto-revert-mode 1)  ;; Keep buffers up to date with their files.
  (setq global-auto-revert-non-file-buffers t)   ;; Automatically refresh non-file buffers.
  (setq revert-without-query (list ".")  ; Do not prompt
        auto-revert-stop-on-user-input nil
        auto-revert-verbose t)

#+end_src

*** Backups
Here we are setting up the backup directory as well as making backups version controlled. Its not git version control, but a special emacs way of keeping multiple backups of a file. We also enable auto saving of buffers.
#+begin_src emacs-lisp

  ;; setting the backup dir to trash.

(setq backup-directory-alist '(("." .
                                  (concat (getenv "XDG_DATA_HOME") "/Trash/files")))
        make-backup-files t     ; backup of a file the first time it is saved.
        backup-by-copying t     ; don't clobber symlinks
        version-control t       ; version numbers for backup files
        delete-old-versions t   ; delete excess backup files silently
        kept-old-versions 6     ; oldest versions to keep when a new numbered
        kept-new-versions 9)    ; newest versions to keep when a new numbered

#+end_src

*** Saveplace Mode
=save-place-mode= enables Emacs to remember the last location within a file upon reopening. This feature is particularly beneficial for resuming work at the precise point where you previously left off.
#+begin_src emacs-lisp

  (save-place-mode 1)
  (setq save-place-file (expand-file-name "saveplace" user-emacs-directory))
  (setq save-place-limit 600)

#+end_src

*** Savehist Mode
=savehist= is an Emacs feature that preserves the minibuffer history between sessions. It saves the history of inputs in the minibuffer, such as commands, search strings, and other prompts, to a file. This allows users to retain their minibuffer history across Emacs restarts.
#+begin_src emacs-lisp

  (setq savehist-additional-variables
        '(kill-ring
          command-history
          set-variable-value-history
          custom-variable-history
          query-replace-history
          read-expression-history
          minibuffer-history
          read-char-history
          face-name-history
          bookmark-history
          file-name-history))

  (setq history-length 300)
  (setq kill-ring-max 25)
  (put 'minibuffer-history         'history-length 50)
  (put 'file-name-history          'history-length 50)
  (put 'set-variable-value-history 'history-length 25)
  (put 'custom-variable-history    'history-length 25)
  (put 'query-replace-history      'history-length 25)
  (put 'read-expression-history    'history-length 25)
  (put 'read-char-history          'history-length 25)
  (put 'face-name-history          'history-length 25)
  (put 'bookmark-history           'history-length 25)
  (savehist-mode 1)

#+end_src

We remove text properties for kill ring entries so that the savehist file doesn't get way too large. A large savehist file slows down emacs considerably. =substring-no-properties= removes any text properties from a given string. =kill-ring= is a list of strings; we're using =mapcar= to apply =substring-no-properties= to each string that is currently in the kill ring. The result of the =mapcar call= (i.e. a list of strings without any text properties) is used to override the original value of =kill-ring=. [[https://emacs.stackexchange.com/questions/4187/strip-text-properties-in-savehist][Source]]
#+begin_src emacs-lisp

  (defun unpropertize-kill-ring ()
    (setq kill-ring (mapcar 'substring-no-properties kill-ring)))
  (add-hook 'kill-emacs-hook 'unpropertize-kill-ring)

#+end_src

*** Recentf
=recentf= is an Emacs package that maintains a list of recently accessed files, making it easier to reopen files you have worked on recently.
#+begin_src emacs-lisp

  (setq recentf-max-menu-items 25)
  (recentf-mode 1)
  (setq recentf-max-saved-items 300) ; default is 20
  (setq recentf-auto-cleanup 'mode)

#+end_src

** Theme
#+begin_src emacs-lisp

  (use-package modus-themes
    :config
    (setq modus-themes-italic-constructs t
          modus-themes-bold-constructs t
          modus-themes-mixed-fonts t

          ;; Options for `modus-themes-prompts' are either nil (the
          ;; default), or a list of properties that may include any of those
          ;; symbols: `italic', `WEIGHT'
          modus-themes-prompts '(italic bold)

          ;; The `modus-themes-completions' is an alist that reads two
          ;; keys: `matches', `selection'.  Each accepts a nil value (or
          ;; empty list) or a list of properties that can include any of
          ;; the following (for WEIGHT read further below):
          ;;
          ;; `matches'   :: `underline', `italic', `WEIGHT'
          ;; `selection' :: `underline', `italic', `WEIGHT'
          modus-themes-completions
          '((matches . (extrabold))
            (selection . (semibold italic text-also)))

          ;; Remove the border
          ;; Make the fringe invisible
          modus-themes-common-palette-overrides
          '((border-mode-line-active unspecified)
            (border-mode-line-inactive unspecified)
            (fringe unspecified))

          modus-themes-to-toggle
          '(modus-operandi-tinted modus-vivendi-tinted)

          modus-themes-headings
          '((1 . (variable-pitch 1.5))
            (2 . (1.3))
            (3 . (1.1))
            (agenda-date . (1.3))
            (agenda-structure . (variable-pitch light 1.8))
            (t . (1.1))))

    (load-theme 'modus-vivendi-tinted t))

  ;; adding padding to ui elements to make doing tasks feel more comfortable
  (use-package spacious-padding
    :after modus-themes
    :config
    (spacious-padding-mode))

#+end_src

** Which Key
[[https://github.com/justbur/emacs-which-key][Whichkey]] helps showcase the available bindings. Not much to talk about it, it is a nice to have to discover new bindings or to quickly see what the bindings do. Whichkey comes built in to emacs starting =v30=.
#+begin_src emacs-lisp

  (use-package which-key
    :config (which-key-mode)
    :ensure nil
    :custom
    (which-key-side-window-location 'bottom)
    (which-key-sort-order #'which-key-key-order-alpha)
    (which-key-sort-uppercase-first nil)
    (which-key-add-column-padding 1)
    (which-key-max-display-columns nil)
    (which-key-min-display-lines 10)
    (which-key-side-window-slot -10)
    (which-key-side-window-max-height 0.25)
    (which-key-idle-delay 0.3)
    (which-key-max-description-length 25)
    (which-key-allow-imprecise-window-fit nil)
    (which-key-separator " → " ))

#+end_src

* Applications
** Avy
Jumping around in text has never been easier! Use the =s= key in normal mode to see what [[https://github.com/abo-abo/avy][avy]] is capable of. Here we also change the default text face when avy is activated to make it easier to see the jump places. I copied the =avy-jump-org-block= from [[https://www.howardabrams.com/hamacs/ha-org-literate.html][howard abrams]].
#+begin_src emacs-lisp

  (use-package avy
    :commands evil-avy-goto-char-timer
    :custom
    (avy-background t)
    (avy-timeout-seconds 0.5)
    :config
    (defun nox/avy-jump-org-block ()
      "Jump to org block using Avy subsystem."
      (interactive)
      (avy-jump (rx line-start (zero-or-more blank) "#+begin_src")
                :action 'goto-char)
      ;; Jump _into_ the block:
      (forward-line))
    (defun nox/avy-jump-to-link ()
      "Jump to org block using Avy subsystem."
      (interactive)
      (avy-jump (rx (or "http://" "https://")) :action 'goto-char))
    (set-face-attribute 'avy-background-face nil
                        :background 'unspecified))

#+end_src

** Browse URL
Here we customize the default behavior of when emacs tries to open a web url. As a sort of general case, here we have defined functions so that whenever you try to open a web url, a new prompt is displayed asking whether to open a private firefox tab for that url.
#+begin_src emacs-lisp

  (defun nox/browse-url-maybe-privately (url &optional new-window)
    "Ask whether URL should be browsed in a private browsing window."
    (interactive "URL: ")
    (if (y-or-n-p "Private Browsing? ")
        (nox/browse-url-firefox-privately url)
      (browse-url-default-browser url new-window)))

  (defun nox/browse-url-firefox-privately (url &optional new-window)
    "Make firefox open URL in private-browsing window."
    (interactive (browse-url-interactive-arg "URL: "))
    (let ((process-environment (browse-url-process-environment)))
      (apply 'start-process
             (concat "firefox " url)
             nil
             browse-url-firefox-program
             (list "-private-window" url))))

  (defun nox/open-in-reddigg (url &optional new-window)
    "Open the provided url in reddigg"
    (reddigg-view-comments url))

  (defun nox/parse-readwise (url &optional new-window)
    "Extract, decode and open the save URL part from a given Readwise URL."
    (if (string-match "https://wise\\.readwise\\.io/save\\?url=\\(.*\\)" url)
        (nox/browse-url-maybe-privately (url-unhex-string (match-string 1 url)))
      (error "Invalid URL format")))

#+end_src

Now we setup rules for when to use the above defined functions. Here we have reddit for example where ~nox/open-in-reddigg~ is called whenever you try to open a url matching the below described pattern for reddit.
#+begin_src emacs-lisp

  (setq browse-url-handlers
        '(("^https?://www\\.reddit\\.com" . nox/open-in-reddigg)
          ("^https?://arstechnica\\.com" . eww)
          ("^https?://wise\\.readwise\\.io/save\\?url=" . nox/parse-readwise)
          ("." . nox/browse-url-maybe-privately)))

  (setq browse-url-generic-program "firefox")

#+end_src

** Buffer Alist
#+begin_src emacs-lisp

  (setq display-buffer-alist
        '(("\\*Occur\\*"
           (display-buffer-reuse-mode-window)
           (display-buffer-below-selected)
           (dedicated . t)
           (window-height . fit-window-to-buffer))
          ))

#+end_src

** Buffer Commands
Run commands for specific buffer. The name of the buffer is enough to make the following work.
#+begin_src emacs-lisp

  (defun nox/run-commands-for-buffer-names ()
    "Run specific commands for certain buffer names."
    (let ((buffer-name (buffer-name)))
      (cond
       ((string-prefix-p "*ChatGPT" buffer-name)
        ;; make the window dedicated
        (set-window-dedicated-p (selected-window) t))

       ((string= buffer-name "*use-package statistics*")
        (hl-line-mode))

       ((string= buffer-name "*reddigg-comments*")
        (org-appear-mode -1)
        (evil-goto-first-line)
        ;; convert all md links to org links
        (nox/md-to-org-links)
        ;; make the window dedicated
        (set-window-dedicated-p (selected-window) t)
        ;; easier quitting of the window
        (evil-local-set-key 'normal "q" 'kill-current-buffer)
        ;; open all folds
        (org-fold-show-all)
        (read-only-mode))
       )))

  ;; Add the function to hooks
  (add-hook 'buffer-list-update-hook 'nox/run-commands-for-buffer-names)

#+end_src

** Gptel
The =gptel= package for Emacs is a client for OpenAI's GPT models, enabling users to interact with the AI directly within the Emacs environment. It provides a convenient interface for sending prompts and receiving responses, allowing for streamlined integration of AI-assisted writing and code generation. With features like customizable prompts, conversation history, and support for multiple models, =gptel= enhances the productivity of Emacs users by leveraging powerful AI capabilities right in their workflow.
#+begin_src emacs-lisp

  (use-package gptel
    :commands gptel
    :hook
    (gptel-mode . evil-insert-state)
    (gptel-post-stream . gptel-auto-scroll)
    (gptel-post-response-functions . gptel-end-of-response)
    :bind* (("C-c RET" . gptel-send))
    :custom
    (gptel-default-mode 'org-mode)
    (gptel-api-key
     (nth 0 (process-lines "cat"
                           (concat
                            (getenv "HOME")
                            "/.config/sops-nix/secrets/api_keys/openai"))))
    :config
    (gptel-make-kagi "Kagi"
      :key
      (nth 0 (process-lines "cat"
                            (concat
                             (getenv "HOME")
                             "/.config/sops-nix/secrets/api_keys/kagi"))))
    (gptel-make-gemini "Gemini"
      :key
      (nth 0 (process-lines "cat"
                            (concat
                             (getenv "HOME")
                             "/.config/sops-nix/secrets/api_keys/gemini")))
      :stream t))

#+end_src

** Expand Region
#+begin_src emacs-lisp

  (use-package expand-region
    :bind ("C-=" . er/expand-region))

#+end_src

** Elfeed
[[https://github.com/skeeto/elfeed][Elfeed]] is a rss reader for emacs. Here, since elfeed doesn't have a hook for when displaying a feed item, we customize ~elfeed-show-entry-switch~ to make it work like a hook for this particular usecase.
#+begin_src emacs-lisp

  (use-package elfeed
    :commands elfeed
    :custom
    (elfeed-search-filter "@1-weeks-ago +unread")
    :config
    (require 'nano-elfeed)
    (evil-define-key 'normal elfeed-search-mode-map
      (kbd "j") 'nano-elfeed-next-entry
      (kbd "k") 'nano-elfeed-prev-entry)
    (defun nox/elfeed-show (buff)
      (switch-to-buffer buff)
      (display-line-numbers-mode -1)
      (visual-fill-column-mode)
      (elfeed-show-refresh))
    (setq elfeed-show-entry-switch 'nox/elfeed-show))

#+end_src

Here we also setup [[https://github.com/remyhonig/elfeed-org][elfeed org]] to making managing our feed urls that much more easier. Since this uses org file for setting up the feeds, it is a lot more powerful than the default way.
#+begin_src emacs-lisp

  (use-package elfeed-org
    :after elfeed
    :custom
    (rmh-elfeed-org-files `(,(concat nox/emacs-directory "elfeed.org")))
    :config (elfeed-org))

#+end_src

*** Elfeed Tube
[[https://github.com/karthink/elfeed-tube][Elfeed Tube]] improves the already outstanding [[#elfeed][elfeed]] to work even more seamlessly with youtube. It adds thumbnail, transcript, mpv support and so much more to youtube feeds.
#+begin_src emacs-lisp

    (use-package elfeed-tube
      :after elfeed
      :custom
      (elfeed-tube-auto-save-p t)
      (elfeed-tube-fields '(duration thumbnail captions chapters))
      :config
      (elfeed-tube-setup)
      :general
      (:keymaps 'elfeed-show-mode-map :states 'normal
                "F" 'elfeed-tube-mpv-follow-mode
                "M" 'elfeed-tube-mpv
                "M-RET" (lambda () (interactive) (nox/avy-jump-to-link) (browse-url-generic))
                [remap save-buffer] 'elfeed-tube-save)
      (:keymaps 'elfeed-search-mode-map :states 'normal
                "F" 'elfeed-tube-fetch
                [remap elfeed-search-fetch] 'elfeed-update
                [remap save-buffer] 'elfeed-tube-save))

    (use-package elfeed-tube-mpv :after efleed-tube)

#+end_src

*** Reddigg
[[https://github.com/thanhvg/emacs-reddigg][Reddigg]] is an interface for emacs to make viewing reddit inside emacs more well integrated. It uses org mode to display the reddit feed, but here we mostly use it for its ability to show reddit comments as foldedup org headings.
#+begin_src emacs-lisp

  (use-package reddigg :commands reddigg-view-comments)

#+end_src

** Eww
The greatest text based browser to ever exist. /In my opinion!/ Here we are just trying to make it a little better.
#+begin_src emacs-lisp

  (add-hook 'eww-mode-hook (lambda ()
                             (display-line-numbers-mode -1)
                             (visual-fill-column-mode)))

#+end_src

** Mu4e
=Mu4e= is an email client for Emacs that integrates with the Mu email search engine. It allows users to manage and read email directly within Emacs, providing features like search capabilities, threading, and support for multiple mail folders. Mu4e is designed for efficiency and works well with large volumes of email, leveraging Mu's fast indexing. Users can compose, reply, and organize their emails while enjoying the extensibility and customizability of Emacs.
#+begin_src emacs-lisp

  (use-package mu4e
    :ensure nil
    :defer 5
    :hook
    (mu4e-main-mode . visual-fill-column-mode)
    (mu4e-view-mode . visual-fill-column-mode)
    :config
    (mu4e t)
    (setq user-mail-address "adwait@adhk.dev")
    ;; This is set to 't' to avoid mail syncing issues when using mbsync
    (setq mu4e-change-filenames-when-moving t)

    ;; Refresh mail using isync every 5 minutes
    (setq mu4e-update-interval (* 5 60))
    (setq mu4e-get-mail-command "mbsync -a")
    (setq mu4e-maildir "~/Mail/proton")

    (setq mu4e-drafts-folder "/Drafts")
    (setq mu4e-sent-folder   "/Sent")
    (setq mu4e-refile-folder "/Archive")
    (setq mu4e-trash-folder  "/Trash")

    ;; prefer text/plain when viewing mail
    (with-eval-after-load "mm-decode"
      (add-to-list 'mm-discouraged-alternatives "text/html")
      (add-to-list 'mm-discouraged-alternatives "text/richtext" t))

    (setq mu4e-maildir-shortcuts
          '((:maildir "/Archive"                :key ?a)
            (:maildir "/Drafts"                 :key ?d)
            (:maildir "/Inbox"                  :key ?i)
            (:maildir "/Sent"                   :key ?s)
            (:maildir "/Folders/Wisdom Letters" :key ?w))))

#+end_src

** PDF Tools
Viewing pdfs inside emacs. The render is created on-demand and stored in memory. Installed through nixos. Here we set ~pdf-view-themed-minor-mode~ to make it more seamless with rest of emacs. We also hide the cursor in evil normal mode inside ~pdf-view-mode~.
#+begin_src emacs-lisp

  (use-package pdf-tools
    :ensure nil
    :hook
    (pdf-view-mode . (lambda ()
                       (pdf-view-themed-minor-mode)
                       (set (make-local-variable 'evil-normal-state-cursor) (list nil))))
    :mode "\\.pdf\\'"
    :bind (:map pdf-view-mode-map
                ("j" . pdf-view-next-line-or-next-page)
                ("k" . pdf-view-previous-line-or-previous-page)
                ("C-=" . pdf-view-enlarge)
                ("C--" . pdf-view-shrink))
    :config
    (package-initialize)
    (pdf-tools-install)
    (add-to-list 'revert-without-query ".pdf"))

  (use-package org-pdftools
     :ensure nil
     :hook (org-mode . org-pdftools-setup-link))

#+end_src

** Uniline
#+begin_src emacs-lisp

  (use-package uniline :commands uniline-mode)

#+end_src


* Buffer Management
Buffers are an integral part of any emacs workflow. Here we customize it to make it more pleasing to work with.
** Ibuffer
Add icons to ibuffer as well as remove line numbers in ibuffer. Also disable ~visual-line-mode~ since we don't want text wrapping inside ibuffer.
#+begin_src emacs-lisp

  (use-package nerd-icons-ibuffer
    :hook (ibuffer-mode . nerd-icons-ibuffer-mode))

  (use-package ibuffer
    :ensure nil
    :commands ibuffer
    :hook
    (ibuffer-mode . (lambda () (display-line-numbers-mode -1)))
    (ibuffer-mode . (lambda () (visual-line-mode -1))))

#+end_src

** Perspective
[[https://github.com/nex3/perspective-el][Perspective]] provides multiple named workspaces inside emacs. Its similar to how window managers do this. I have improved on this to make easier keybinds for switching my most used perspectives.
#+begin_src emacs-lisp

  (use-package perspective
    :commands (nox/restore-perspectives persp-state-load)
    :custom
    (persp-state-default-file "~/.local/share/persp-state")
    (persp-mode-prefix-key (kbd "C-c b"))
    (persp-modestring-short t)
    (persp-initial-frame-name "1 aurora")
    (persp-modestring-dividers '("" "" ""))
    :config
    (add-hook 'kill-emacs-hook #'persp-state-save))

#+end_src

I have also created a handy function which helps restore the saved perspectives in my workflow. This is a kind of hacky way of restoring perspectives as it doesn't handle if you use multiple emacs frames in your workflow.
#+begin_src emacs-lisp

  (defun nox/restore-perspectives ()
    "Restores the last saved perspective-state and deletes all other frames"
    (interactive)
    (persp-state-load persp-state-default-file)
    (delete-other-frames))

#+end_src

Easier switching of perspectives using simple bindings with follow vim motions keys.
#+begin_src emacs-lisp

  (with-eval-after-load 'evil
    (evil-define-key '(normal insert) 'global
      (kbd "C-S-h") '(lambda () (interactive) (persp-switch-by-number 1))
      (kbd "C-S-j") '(lambda () (interactive) (persp-switch-by-number 2))
      (kbd "C-S-k") '(lambda () (interactive) (persp-switch-by-number 3))
      (kbd "C-S-l") '(lambda () (interactive) (persp-switch-by-number 4))))

#+end_src

* Development
** Completions
Using [[https://github.com/minad/corfu][corfu]] for completions. Corfu enhances in-buffer completion with a small completion popup. It is the minimalistic inbuffer completion counterpart of the [[#vertico][vertico]] minibuffer UI.
#+begin_src emacs-lisp

  (use-package corfu
    :hook (on-first-input . global-corfu-mode)
    :custom
    (corfu-cycle t)                 ;; Enable cycling for `corfu-next/previous'
    (corfu-auto t)                  ;; Enable auto completion
    (corfu-auto-prefix 2)           ;; Enable auto completion
    (corfu-auto-delay 0.24)         ;; Enable auto completion
    (corfu-preview-current 'insert) ;; Disable current candidate preview
    (corfu-on-exact-match nil)      ;; Configure handling of exact matches
    (corfu-scroll-margin 5)         ;; Use scroll margin
    (corfu-quit-at-boundary 'separator)
    :bind
    (:map corfu-map
          ("M-SPC" . corfu-insert-separator))
    :config
    (add-to-list 'corfu--frame-parameters '(alpha-background . 0.9))

    (add-to-list
     'completion-category-overrides `(lsp-capf (styles ,@completion-styles)))

    (add-hook 'evil-insert-state-exit-hook #'corfu-quit)
    (global-corfu-mode))

#+end_src

Here we are adding icons to corfu.
#+begin_src emacs-lisp

  (use-package nerd-icons-corfu
    :after corfu
    :defer 5
    :config
    (add-to-list 'corfu-margin-formatters #'nerd-icons-corfu-formatter))

#+end_src

Here we are enabling ~corfu-history~. This allows sorting of completions by the frequency of their use.
#+begin_src emacs-lisp

  (use-package corfu-history
    :after (corfu savehist)
    :ensure nil
    :hook
    (corfu-mode . corfu-history-mode)
    :config
    (add-to-list 'savehist-additional-variables 'corfu-history))

#+end_src

Here we setup popupinfo for the completion candidate.
#+begin_src emacs-lisp

  (use-package corfu-popupinfo
    :ensure nil
    :hook
    (corfu-mode . corfu-popupinfo-mode)
    :config
    ;; popup info delay
    (setq corfu-popupinfo-delay '(0.5 . 1.0)))

#+end_src

Changing emacs settings so that we remove commands that do not apply to the current mode. We also disable the ispell completion function.
#+begin_src emacs-lisp

  (use-package emacs
    :ensure nil
    :custom
    ;; Emacs 30 and newer: Disable Ispell completion function. As an alternative,
    ;; try `cape-dict'.
    (text-mode-ispell-word-completion nil)

    ;; Hide commands in M-x which do not apply to the current mode.  Corfu
    ;; commands are hidden, since they are not used via M-x. This setting is
    ;; useful beyond Corfu.
    (read-extended-command-predicate #'command-completion-default-include-p))

#+end_src

** Formatters
Here we use [[https://github.com/radian-software/apheleia][apheleia]] for code formatting. Formats code asynchronously and only after ~after-save-hook~.
#+begin_src emacs-lisp

  (use-package apheleia
    :hook
    (prog-mode . apheleia-mode)
    :config
    (push '(nix-ts-mode . nixfmt) apheleia-mode-alist))

#+end_src

** Flymake
Flymake is an on-the-fly syntax checking extension that provides real-time feedback about errors and warnings in your code as you write. This can greatly enhance your coding experience by catching issues early. The configuration below activates Flymake mode in programming buffers.
#+begin_src emacs-lisp

  (use-package flymake
    :ensure nil
    :hook (prog-mode . flymake-mode)
    :custom
    (flymake-margin-indicators-string
     '((error "!»" compilation-error) (warning "»" compilation-warning)
  	 (note "»" compilation-info))))

#+end_src

** Language Servers
Support of LSPs inside emacs. Also improve the general ui surrounding LSPs.
#+begin_src emacs-lisp

  (use-package lsp-mode
    :commands lsp
    :hook
    (bash-ts-mode . lsp-deferred)                  ;; Enable LSP for Bash
    (go-ts-mode . lsp-deferred)                    ;; Enable LSP for Go
    (lsp-mode . lsp-enable-which-key-integration)  ;; Integrate with Which Key
    :custom
    (lsp-keymap-prefix "C-c l")                    ;; Set the prefix for LSP commands
    (lsp-inlay-hint-enable t)                      ;; Enable inlay hints
    (lsp-completion-provider :none)                ;; Disable default completion provider
    (lsp-log-io nil)                               ;; Disable IO logging for speed
    (lsp-idle-delay 0)                             ;; Set the delay for LSP to 0
    (lsp-keep-workspace-alive nil)                 ;; Disable keeping workspace alive
    (lsp-session-file (locate-user-emacs-file ".lsp-session")) ;; Session file location

    ;; Core settings
    (lsp-enable-xref t)                            ;; Enable cross-references.
    (lsp-auto-configure t)                         ;; Automatically configure LSP.
    (lsp-enable-links nil)                         ;; Disable links.
    (lsp-eldoc-enable-hover t)                     ;; Enable ElDoc hover.
    (lsp-enable-file-watchers nil)                 ;; Disable file watchers.
    (lsp-enable-folding nil)                       ;; Disable folding.
    (lsp-enable-imenu t)                           ;; Enable Imenu support.
    (lsp-enable-indentation nil)                   ;; Disable indentation.
    (lsp-enable-on-type-formatting nil)            ;; Disable on-type formatting.
    (lsp-enable-suggest-server-download t)         ;; Enable server download suggestion.
    (lsp-enable-symbol-highlighting t)             ;; Enable symbol highlighting.
    (lsp-enable-text-document-color nil)           ;; Disable text document color.

    ;; Modeline settings
    (lsp-modeline-code-actions-enable nil)         ;; Keep modeline clean.
    (lsp-modeline-diagnostics-enable nil)          ;; Use `flymake' instead.
    (lsp-modeline-workspace-status-enable t)       ;; Display "LSP" in the modeline
    (lsp-signature-doc-lines 1)                    ;; Limit echo area to one line.
    (lsp-eldoc-render-all nil)                     ;; Render only signature messages.

    ;; Completion settings
    (lsp-completion-enable t)                      ;; Enable completion.
    (lsp-completion-enable-additional-text-edit t) ;; Enable additional text edits.
    (lsp-enable-snippet nil)                       ;; Disable snippets
    (lsp-completion-show-kind t)                   ;; Show kind in completions.
    (lsp-completion-show-detail nil)               ;; Disable long signature details

    ;; Lens settings
    (lsp-lens-enable t)                            ;; Enable lens support.

    ;; Headerline settings
    (lsp-headerline-breadcrumb-icons-enable t)            ;; Enable icons
    (lsp-headerline-breadcrumb-enable-diagnostics nil)    ;; Disable diagnostics
    (lsp-headerline-breadcrumb-enable-symbol-numbers nil) ;; Disable numbers

    ;; Disable semantic tokens.
    (lsp-semantic-tokens-enable nil)

    ;; nix
    (lsp-nix-nixd-nixpkgs-expr "import <nixpkgs> { }")
    (lsp-nix-nixd-nixos-options-expr
     "(builtins.getFlake \"/home/nox/aurora/flakes\").nixosConfigurations.anomaly.options")
    (lsp-nix-nixd-home-manager-options-expr
     "(builtins.getFlake \"/home/nox/aurora/flakes\").homeConfigurations.nox.options")
    )

  (with-eval-after-load 'lsp-mode
    ;; need lsp-treemacs to make icons in breadcrumb to work
    (use-package lsp-treemacs :after lsp-mode)
    ;; lsp theming
    (set-face-attribute 'lsp-face-highlight-textual nil
                        :underline t
                        :inherit nil))

  (use-package lsp-ui
    :hook
    (lsp-mode . lsp-ui-mode)
    :custom
    (lsp-ui-doc-show-with-cursor nil)
    (lsp-ui-doc-show-with-mouse t)
    (lsp-ui-doc-position 'at-point)
    (lsp-ui-doc-border "#585b70")
    (lsp-ui-doc-enable t))

#+end_src

** Language Configs
Adding major modes for languages that emacs doesn't support out of the box. We use ~lsp-deferred~ instead of directly calling ~lsp~ to prevent lsp from starting when quickly switching buffers.
#+begin_src emacs-lisp

  (use-package nix-mode :mode "\\.nix\\'")
  (use-package nushell-mode :mode "\\.nu\\'")

  (use-package nix-ts-mode :hook (nix-ts-mode . lsp-deferred))
  (use-package nushell-ts-mode :hook (nushell-ts-mode . lsp-deferred))

#+end_src

** Ligatures
Pretty fontification.
#+begin_src emacs-lisp

  (use-package ligature
    :hook (on-first-input . global-ligature-mode)
    :config
    ;; Enable the "www" ligature in every possible major mode
    (ligature-set-ligatures 't '("www"))
    ;; Enable traditional ligature support in eww-mode, if the
    ;; `variable-pitch' face supports it
    (ligature-set-ligatures 'eww-mode '("ff" "fi" "ffi"))
    ;; Enable all Cascadia Code ligatures in programming modes
    (ligature-set-ligatures '(prog-mode org-mode)
                            '("|||>" "<|||" "<==>" "<!--" "####" "~~>" "***" "||=" "||>"
                              ":::" "::=" "=:=" "===" "==>" "=!=" "=>>" "=<<" "=/=" "!=="
                              "!!." ">=>" ">>=" ">>>" ">>-" ">->" "->>" "-->" "---" "-<<"
                              "<~~" "<~>" "<*>" "<||" "<|>" "<$>" "<==" "<=>" "<=<" "<->"
                              "..." "+++" "/==" "///" "_|_" "www" "&&" "^=" "~~" "~@" "~="
                              "~>" "~-" "**" "*>" "*/" "||" "|}" "|]" "=>" "!=" "!!" ">:"
                              ">=" ">>" ">-" "-~" "-|" "->" "--" "-<" "<~" "<*" "<|" "<:"
                              "<$" "<=" "<>" "<-" "<<" "<+" "</" "#{" "#[" "#:" "#=" "#!"
                              "##" "#(" "#?" "#_" "%%" ".=" ".-" ".." ".?" "+>" "++" "?:"
                              "?=" "?." "??" ";;" "/*" "/=" "/>" "//" "__" "~~" "(*" "*)"
                              "<--" "<-<" "<<=" "<<-" "<<<" "<+>" "</>" "###" "#_(" "..<"
                              "\\\\" "://")))

#+end_src

** Rainbow Delimiters
Add color coded delimeters. Also highlight any delimeters which is mismatched.
#+begin_src emacs-lisp

  (use-package rainbow-delimiters
    :hook
    (prog-mode . rainbow-delimiters-mode)
    :config
    (setq rainbow-delimiters-max-face-count 5))

#+end_src


** Projectile
Project management at its finest.
#+begin_src emacs-lisp

  (use-package projectile
    :bind-keymap
    ("C-c p" . projectile-command-map)
    :config
    (projectile-mode 1))

#+end_src

** SOPS
#+begin_src emacs-lisp

  ;; (use-package sops
  ;;   :mode "\\.sops.yaml\\'"
  ;;   :hook
  ;;   (sops-mode . yaml-ts-mode)
  ;;   :bind (("C-c C-c" . sops-save-file)
  ;;          ("C-c C-k" . sops-cancel)
  ;;          ("C-c C-d" . sops-edit-file))
  ;;   :config
  ;;   (global-sops-mode))

#+end_src

** Treesitter
Pretty syntax that I can look at all day long. This also allows a lot of niceties when moving around the code.
#+begin_src emacs-lisp

  (use-package treesit-auto
    :hook
    (on-first-file . global-treesit-auto-mode)
    :custom
    (treesit-auto-install 'prompt)
    :config
    (treesit-auto-add-to-auto-mode-alist 'all))

#+end_src

* Dired
The best file manager to exist. Dired is a deep rabbithole that you can spend multiple hours going into. Here we have setup some nicities that will help you in your journey of learning the world of dired.
#+begin_src emacs-lisp

  (use-package dired
    :after evil
    :ensure nil
    :commands (dired dired-jump)
    :hook
    (dired-mode . hl-line-mode)
    ;; To hide dot-files by default
    (dired-mode . dired-omit-mode)
    ;; removing line numbers
    (dired-mode . (lambda () (display-line-numbers-mode -1)))
    (image-mode . (lambda () (display-line-numbers-mode -1)))
    :custom
    ;; hide files/directories starting with "." in dired-omit-mode
    (dired-omit-files (rx (seq bol ".")))
    ;; Display files in a human-readable format and group directories first
    ;; Also remove owner and group information
    (dired-listing-switches "-agho --group-directories-first")

    ;; Enable "do what I mean" for target directories
    (dired-dwim-target t)

    ;; Close the previous buffer when opening a new `dired' instance
    (dired-kill-when-opening-new-dired-buffer t)
    :config
    (setq dired-free-space nil
          dired-deletion-confirmer 'y-or-n-p
          dired-filter-verbose nil
          dired-clean-confirm-killing-deleted-buffers nil
          dired-recursive-deletes 'top
          dired-recursive-copies  'always
          dired-create-destination-dirs 'ask)
    (evil-define-key 'normal dired-mode-map
      (kbd "h") 'dired-up-directory
      (kbd "l") 'dired-open-file))


#+end_src

Adding icons to dired.
#+begin_src emacs-lisp

  (use-package all-the-icons-dired
    :hook (dired-mode . all-the-icons-dired-mode)
    :commands (dired dired-jump))

#+end_src

** Diredfl
Pretty colors inside dired. This is mostly used for everything except for file name coloring. That is done using [[#dired-rainbow][dired rainbow]].
#+begin_src emacs-lisp

  (use-package diredfl
    :hook (dired-mode . diredfl-mode)
    :commands (dired dired-jump))

#+end_src

** Dired Open
Opening specific files with specific programs.
#+begin_src emacs-lisp

  (use-package dired-open
    :commands (dired dired-jump)
    :config
    (setq dired-open-extensions '(("gif" . "imv")
                                  ("jpg" . "imv")
                                  ("webp" . "imv")
                                  ("png" . "imv")
                                  ("mkv" . "mpv")
                                  ("mp4" . "mpv"))))

#+end_src

** Dired Rainbow
Here we setup file name coloring. This is just the defaults and need to be updated to use catppuccin colors.
#+begin_src emacs-lisp

  (use-package dired-rainbow
    :commands (dired dired-jump)
    :config
    (dired-rainbow-define-chmod directory "#6cb2eb" "d.*")
    (dired-rainbow-define-chmod executable-unix "#38c172" "-.*x.*")
    (dired-rainbow-define html "#eb5286" ("css" "less" "sass" "scss" "htm" "html" "jhtm" "mht" "eml" "mustache" "xhtml"))
    (dired-rainbow-define xml "#f2d024" ("xml" "xsd" "xsl" "xslt" "wsdl" "bib" "json" "msg" "pgn" "rss" "yaml" "yml" "rdata"))
    (dired-rainbow-define document "#9561e2" ("docm" "doc" "docx" "odb" "odt" "pdb" "pdf" "ps" "rtf" "djvu" "epub" "odp" "ppt" "pptx"))
    (dired-rainbow-define markdown "#ffed4a" ("org" "etx" "info" "markdown" "md" "mkd" "nfo" "pod" "rst" "tex" "textfile" "txt"))
    (dired-rainbow-define database "#6574cd" ("xlsx" "xls" "csv" "accdb" "db" "mdb" "sqlite" "nc"))
    (dired-rainbow-define media "#de751f" ("mp3" "mp4" "MP3" "MP4" "avi" "mpeg" "mpg" "flv" "ogg" "mov" "mid" "midi" "wav" "aiff" "flac"))
    (dired-rainbow-define image "#f66d9b" ("tiff" "tif" "cdr" "gif" "ico" "jpeg" "jpg" "png" "psd" "eps" "svg"))
    (dired-rainbow-define log "#c17d11" ("log"))
    (dired-rainbow-define shell "#f6993f" ("awk" "bash" "bat" "sed" "sh" "zsh" "vim"))
    (dired-rainbow-define interpreted "#38c172" ("py" "ipynb" "rb" "pl" "t" "msql" "mysql" "pgsql" "sql" "r" "clj" "cljs" "scala" "js"))
    (dired-rainbow-define compiled "#4dc0b5" ("asm" "cl" "lisp" "el" "c" "h" "c++" "h++" "hpp" "hxx" "m" "cc" "cs" "cp" "cpp" "go" "f" "for" "ftn" "f90" "f95" "f03" "f08" "s" "rs" "hi" "hs" "pyc" ".java"))
    (dired-rainbow-define executable "#8cc4ff" ("exe" "msi"))
    (dired-rainbow-define compressed "#51d88a" ("7z" "zip" "bz2" "tgz" "txz" "gz" "xz" "z" "Z" "jar" "war" "ear" "rar" "sar" "xpi" "apk" "xz" "tar"))
    (dired-rainbow-define packaged "#faad63" ("deb" "rpm" "apk" "jad" "jar" "cab" "pak" "pk3" "vdf" "vpk" "bsp"))
    (dired-rainbow-define encrypted "#ffed4a" ("gpg" "pgp" "asc" "bfe" "enc" "signature" "sig" "p12" "pem"))
    (dired-rainbow-define fonts "#6cb2eb" ("afm" "fon" "fnt" "pfb" "pfm" "ttf" "otf"))
    (dired-rainbow-define partition "#e3342f" ("dmg" "iso" "bin" "nrg" "qcow" "toast" "vcd" "vmdk" "bak"))
    (dired-rainbow-define vc "#0074d9" ("git" "gitignore" "gitattributes" "gitmodules")))

#+end_src

** Peep Dired
File previews in dired.
#+begin_src emacs-lisp

  (use-package peep-dired
    :commands (dired dired-jump)
    :config
    (evil-define-key 'normal peep-dired-mode-map
      (kbd "j") 'peep-dired-next-file
      (kbd "k") 'peep-dired-prev-file))

#+end_src

* Fonts
** Setting Font Faces
Here we setup the default font faces for =variable-pitch= as well as =fixed-pitch=. Throughout emacs we mostly use =fixed-pitch= but enable =variable-pitch= where deemed necessary like in org files.
#+begin_src emacs-lisp

  (set-face-attribute 'variable-pitch nil
                      :family "Inter"
                      :height 140
                      :weight 'medium)
  (set-face-attribute 'fixed-pitch nil
                      :family "CaskaydiaCove Nerd Font"
                      :height 140
                      :weight 'medium)
  (set-face-attribute 'default nil :inherit 'fixed-pitch)
  (set-face-attribute 'fixed-pitch-serif nil :inherit 'fixed-pitch :family 'unspecified)

  ;; setting the emoji font family
  ;; https://emacs.stackexchange.com/a/80186
  (set-fontset-font t 'emoji
                    '("Apple Color Emoji" . "iso10646-1") nil 'prepend)


  ;; italic comments and keywords
  (set-face-attribute 'font-lock-comment-face nil
                      :italic t)

  ;; setting the line spacing
  (setq-default line-spacing 0.16)

#+end_src

This sets the default font on all graphical frames created after restarting Emacs. Does the same thing as ~set-face-attribute default~ above, but emacsclient fonts are not right unless I also add this method of setting the default font.
#+begin_src emacs-lisp

  (add-to-list 'default-frame-alist '(font . "CaskaydiaCove Nerd Font-14"))

#+end_src

** Zooming In/Out
Binding CTRL plus +/- for zooming in/out.
#+begin_src emacs-lisp

  (global-set-key (kbd "C-=") 'text-scale-increase)
  (global-set-key (kbd "C--") 'text-scale-decrease)
  (global-set-key (kbd "<C-wheel-up>") 'text-scale-increase)
  (global-set-key (kbd "<C-wheel-down>") 'text-scale-decrease)

#+end_src

* Git
** Diff HL
[[https://github.com/dgutov/diff-hl][Diff-hl]] highlights uncommitted changes on the left side of the window area also known as the =gutter=, allowing you to jump between and revert them selectively. In buffers controlled by git, you can also stage and unstage the changes.
#+begin_src emacs-lisp

  (use-package diff-hl
    :hook (vc-dir-mode . diff-hl-mode)
    :hook (lsp-mode . diff-hl-mode)
    :commands
    (diff-hl-stage-current-hunk
     diff-hl-revert-hunk
     diff-hl-next-hunk
     diff-hl-previous-hunk)
    :custom
    ;; Set the side for diff indicators.
    (diff-hl-side 'left)
    ;; Customize symbols for each change type.
    (diff-hl-margin-symbols-alist '((insert . "│")
                                    (delete . "-")
                                    (change . "│")
                                    (unknown . "?")
                                    (ignored . "i")))
    (diff-hl-show-staged-changes nil)
    :config
    ;; implements highlighting changes on the fly.
    (diff-hl-flydiff-mode 1)
    ;; changes the highlighting function to use the margin instead of the fringe.
    (diff-hl-margin-mode 1))

#+end_src

** Magit
=From the readme:= [[https://github.com/magit/magit][Magit]] is an interface to the version control system =Git=, implemented as an =Emacs= package. Magit aspires to be a complete =Git porcelain=. While we cannot ~yet~ claim that Magit wraps and improves upon each and every Git command, it is complete enough to allow even experienced Git users to perform almost all of their daily version control tasks directly from within Emacs. While many fine Git clients exist, only Magit and Git itself deserve to be called porcelains.
#+begin_src emacs-lisp

  (use-package transient)
  (use-package magit
    :commands magit-status
    :hook
    (magit-pre-refresh . diff-hl-magit-pre-refresh)
    (magit-post-refresh . diff-hl-magit-post-refresh))

#+end_src

* Helpful
Better help pages.
#+begin_src emacs-lisp

  (use-package helpful
    :commands
    (helpful-callable helpful-variable helpful-key helpful-command helpful-at-point)
    :hook
    (helpful-mode . (lambda ()
                      (set-window-dedicated-p (selected-window) t)))
    :custom
    (helpful-max-buffers 2)
    :bind
    ([remap describe-function] . helpful-callable)
    ([remap describe-command] . helpful-command)
    ([remap describe-key] . helpful-key)
    ([remap describe-variable] . helpful-variable)
    ([remap view-hello-file] . helpful-at-point))

#+end_src

* Minibuffer Completion
** Consult
Consult provides powerful completion and narrowing commands for Emacs. It integrates well with other completion frameworks like [[#vertico][vertico]], enabling features like previews and enhanced register management. It's useful for navigating buffers, files, and xrefs with ease.
#+begin_src emacs-lisp
  (use-package consult
    ;;:after perspective
    :bind
    ([remap bookmark-jump] . consult-bookmark)
    :commands
    (consult-ripgrep
     consult-buffer
     consult-imenu
     consult-mode-command
     consult-yank-from-kill-ring)
    :hook (completion-list-mode . consult-preview-at-point-mode)
    :config
    ;; persp with consult
    ;;(consult-customize consult--source-buffer :hidden t :default nil)
    ;;(add-to-list 'consult-buffer-sources persp-consult-source)

    (setq register-preview-delay 0.5
          register-preview-function #'consult-register-format)

    (advice-add #'register-preview :override #'consult-register-window)

    ;; Use Consult to select xref locations with preview
    (setq xref-show-xrefs-function #'consult-xref
          xref-show-definitions-function #'consult-xref)

    (consult-customize
     consult-theme :preview-key '(:debounce 0.2 any)
     consult-ripgrep consult-git-grep consult-grep
     consult-bookmark consult-recent-file consult-xref
     consult--source-bookmark consult--source-file-register
     consult--source-recent-file consult--source-project-recent-file
     ;; :preview-key "M-."
     :preview-key '(:debounce 0.4 any))

    (setq consult-narrow-key "."))

#+end_src

** Embark
Embark provides a powerful contextual action menu for Emacs, allowing you to perform various operations on completion candidates and other items. It extends the capabilities of completion frameworks by offering direct actions on the candidates. Just =C-'= over any text, explore it.
#+begin_src emacs-lisp

  (use-package embark
    ;; using bind* to override other bindings
    :bind*
    (("C-'" . embark-act)         ;; pick some comfortable binding
     ("C-;" . embark-dwim)        ;; good alternative: M-.
     ("C-h B" . embark-bindings)) ;; alternative for `describe-bindings'
    :custom
    ;; Optionally replace the key help with a completing-read interface
    (prefix-help-command #'embark-prefix-help-command)
    :config
    ;; Hide the mode line of the Embark live/completions buffers
    (add-to-list 'display-buffer-alist
                 '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                   nil
                   (window-parameters (mode-line-format . none)))))

  ;; Consult users will also want the embark-consult package.
  (use-package embark-consult
    :hook
    (embark-collect-mode . consult-preview-at-point-mode))

#+end_src

** Extended Settings
#+begin_src emacs-lisp

  (use-package emacs
    :ensure nil
    :custom
    ;; Hide commands in M-x which do not work in the current mode.  Vertico
    ;; commands are hidden in normal buffers. This setting is useful beyond
    ;; Vertico.
    (read-extended-command-predicate #'command-completion-default-include-p)
    :init
    ;; Add prompt indicator to `completing-read-multiple'.
    ;; We display [CRM<separator>], e.g., [CRM,] if the separator is a comma.
    (defun crm-indicator (args)
      (cons (format "[CRM%s] %s"
                    (replace-regexp-in-string
                     "\\`\\[.*?]\\*\\|\\[.*?]\\*\\'" ""
                     crm-separator)
                    (car args))
            (cdr args)))
    (advice-add #'completing-read-multiple :filter-args #'crm-indicator)

    ;; Do not allow the cursor in the minibuffer prompt
    (setq minibuffer-prompt-properties
          '(read-only t cursor-intangible t face minibuffer-prompt))
    (add-hook 'minibuffer-setup-hook #'cursor-intangible-mode))

#+end_src

** Icons
#+begin_src emacs-lisp

  (use-package nerd-icons-completion
    :after (:all nerd-icons marginalia)
    :hook
    (marginalia-mode . nerd-icons-completion-marginalia-setup)
    :config
    (nerd-icons-completion-mode))

#+end_src

** Marginalia
Marginalia enhances the completion experience in Emacs by adding additional context to the completion candidates. This includes helpful annotations such as documentation and other relevant information, making it easier to choose the right option.
#+begin_src emacs-lisp

  (use-package marginalia
    :after vertico
    :config
    (marginalia-mode))

#+end_src

** Oderless
Orderless enhances completion in Emacs by allowing flexible pattern matching. It works seamlessly with [[#vertico][vertico]], enabling you to use partial strings and regular expressions to find files, buffers, and commands more efficiently. This combination provides a powerful and customizable completion experience.
#+begin_src emacs-lisp

  (use-package orderless
    :after vertico
    :custom
    (completion-styles '(orderless basic))
    (completion-category-defaults nil)
    (completion-category-overrides '((file (styles partial-completion))))
    (orderless-matching-styles
     '(orderless-literal
       orderless-prefixes
       orderless-initialism
       orderless-regexp
       orderless-flex
       ;; orderless-strict-leading-initialism
       ;; orderless-strict-initialism
       ;; orderless-strict-full-initialism
       ;; orderless-without-literal          ; Recommended for dispatches instead
       )))

#+end_src

** Vertico
Vertico enhances the completion experience in Emacs by providing a vertical selection interface for both buffer and minibuffer completions. Unlike traditional minibuffer completion, which displays candidates
in a horizontal format, Vertico presents candidates in a vertical list, making it easier to browse and select from multiple options.

In buffer completion, =switch-to-buffer= allows you to select from open buffers. Vertico streamlines this process by displaying the buffer list in a way that improves visibility and accessibility. This is particularly useful when you have many buffers open, allowing you to quickly find the one you need.

In minibuffer completion, such as when entering commands or file paths, Vertico helps by showing a dynamic list of potential completions, making it easier to choose the correct one without typing out the entire string.
#+begin_src emacs-lisp

  (use-package compat :after vertico)
  (use-package vertico
    :hook
    (on-first-input . vertico-mode)
    :custom
    (vertico-count 13)
    (vertico-resize t)
    (vertico-cycle t)
    :bind (:map vertico-map
                ("C-j" . vertico-next)
                ("C-k" . vertico-previous)
                ("M-<return>" . vertico-exit-input)
                ("ESC" . vertico-exit))
    :config
    ;; Add » before the selected completion.
    (advice-add #'vertico--format-candidate :around
                (lambda (orig cand prefix suffix index _start)
                  (setq cand (funcall orig cand prefix suffix index _start))
                  (concat
                   (if (= vertico--index index)
                       (propertize "» " 'face 'vertico-current)
                     "  ")
                   cand))))

  ;; Configure directory extension.
  (use-package vertico-directory
    :after vertico
    :ensure nil
    ;; More convenient directory navigation commands
    :bind (:map vertico-map
                ("RET" . vertico-directory-enter)
                ("DEL" . vertico-directory-delete-char))
    ;; Tidy shadowed file names
    :hook (rfn-eshadow-update-overlay . vertico-directory-tidy))
#+end_src


* Org Mode
Org-mode is a powerful system for organizing and managing your notes, tasks, and documents in plain text. It offers features like task management, outlining, scheduling, and much more, making it a versatile tool for productivity.
** Better Fonts
#+begin_src emacs-lisp

  (defun nox/org-font-setup ()
    ;; Ensure that anything that should be fixed-pitch in Org files appears that way
    (set-face-attribute 'org-block-begin-line nil
                        :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-block-end-line nil
                        :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-meta-line nil
                        :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-document-info-keyword nil
                        :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-special-keyword nil
                        :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-code nil
                        :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-cite nil
                        :underline nil)

    (set-face-attribute 'org-verbatim nil
                        :inherit 'variable-pitch)

    (set-face-attribute 'line-number-current-line nil
                        :inherit 'fixed-pitch)
    (set-face-attribute 'org-block nil
                        :inherit 'fixed-pitch)

    (set-face-attribute 'font-lock-string-face nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-table    nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-formula  nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch)
    (set-face-attribute 'line-number  nil :inherit 'fixed-pitch)

    ;; disable large title
    (set-face-attribute 'org-document-title nil :height 'unspecified))

#+end_src

** Org Roam
#+begin_src emacs-lisp

  (use-package org-roam
    :commands
    (org-roam-capture
     org-roam-dailies-capture-today
     org-roam-node-find
     nox/org-roam-capture-tasks
     org-roam-goto-today)
    :custom
    (org-roam-directory nox/notes-directory)
    (org-roam-dailies-directory "Logs/")
    (org-roam-completion-everywhere t)

#+end_src

Here we are setting up capture templates for org-roam-capture.
#+begin_src emacs-lisp

  ;; see help for format-time-string function for time templates
  (org-roam-capture-templates
   '(("d" "default" item "%?%i"
      :if-new (file+head+olp
               "%<%Y%m%d%H%M%S>-${slug}.org"
               "#+title: ${title}\n#+date: [%<%Y-%m-%d %a %H:%M>]\n#+category: note"
               ("${title}"))
      :unnarrowed t
      :kill-buffer t)
     ("l" "linked" item "%?\n%a"
      :if-new (file+head+olp
               "%<%Y%m%d%H%M%S>-${slug}.org"
               "#+title: ${title}\n#+date: [%<%Y-%m-%d %a %H:%M>]\n#+category: note"
               ("${title}"))
      :unnarrowed t
      :kill-buffer t)))

#+end_src

Here we are setting up capture  templates for org-roam-dailies.
#+begin_src emacs-lisp

  (org-roam-dailies-capture-templates
   '(("d" "default" item "*%<%H:%M>* %?"
      :if-new (file+head+olp
               "%<%Y-%m-%d>.org"
               "#+title: %<%Y-%m-%d>\n#+date: [%<%Y-%m-%d %a %H:%M>]\n#+category: daily"
               ("%<%B %d, %Y>"))
      :unnarrowed t
      :kill-buffer t)
   ("l" "linked" item "*%<%H:%M>* %?\n%a"
    :if-new (file+head+olp
             "%<%Y-%m-%d>.org"
             "#+title: %<%Y-%m-%d>\n#+date: [%<%Y-%m-%d %a %H:%M>]\n#+category: daily"
             ("%<%B %d, %Y>"))
    :unnarrowed t
    :kill-buffer t)))

#+end_src

Now anything else that we want to be loaded when org-roam loads go here.
#+begin_src emacs-lisp

  :config
  (org-roam-db-autosync-mode)
  ;; auto insert mode
  (add-hook 'org-capture-mode-hook 'evil-insert-state)

#+end_src

These following are the custom capture lists that I have made for org roam.
#+begin_src emacs-lisp

  (defun nox/org-roam-capture-tasks ()
    "Custom capture list for org roam"
    (interactive)
    (org-roam-capture-
     :node (org-roam-node-create)
     :templates '(
                  ("t" "General Task"
                   entry "*** TODO %?"
                   :kill-buffer t
                   :if-new (file+head+olp
                            "Inbox/tasks.org"
                            "#+title: Tasks\n#+category: tasks"
                            ("Tasks" "General Tasks")))
                  ("l" "Linked Task"
                   entry "*** TODO %?\n%a"
                   :kill-buffer t
                   :if-new (file+head+olp
                            "Inbox/tasks.org"
                            "#+title: Tasks\n#+category: tasks"
                            ("Tasks" "Linked Tasks")))
                  ))))

#+end_src


The properties drawer that org roam uses is intended to only be used by org roam. So here we are hiding the property drawer but also adding some helpful commands to show it when we want. This code snippet comes from [[https://github.com/org-roam/org-roam/wiki/User-contributed-Tricks#hiding-the-properties-drawer][org roam wiki]].
#+begin_src emacs-lisp

  (defun nox/org-hide-properties ()
    "Hide all org-mode headline property drawers in buffer. Could be slow if it has a lot of overlays."
    (interactive)
    (save-excursion
      (goto-char (point-min))
      (while (re-search-forward
              "^ *:properties:\n\\( *:.+?:.*\n\\)+ *:end:\n" nil t)
        (let ((ov_this (make-overlay (match-beginning 0) (match-end 0))))
          (overlay-put ov_this 'display "")
          (overlay-put ov_this 'hidden-prop-drawer t))))
    (put 'org-toggle-properties-hide-state 'state 'hidden))

  (defun nox/org-show-properties ()
    "Show all org-mode property drawers hidden by org-hide-properties."
    (interactive)
    (remove-overlays (point-min) (point-max) 'hidden-prop-drawer t)
    (put 'org-toggle-properties-hide-state 'state 'shown))

  (defun nox/org-toggle-properties ()
    "Toggle visibility of property drawers."
    (interactive)
    (if (eq (get 'org-toggle-properties-hide-state 'state) 'hidden)
        (nox/org-show-properties)
      (nox/org-hide-properties)))

  ;; call org-hide-properties when inside org mode and org capture
  (add-hook 'org-mode-hook 'nox/org-hide-properties)
  (add-hook 'org-capture-mode-hook 'nox/org-hide-properties)

#+end_src

Similarly, we add a new field to listing of org roam nodes. Now ~org-roam-node-find~ also shows the number of backlinks as well as modification time for a given node. Source is a combination of [[https://github.com/org-roam/org-roam/wiki/User-contributed-Tricks#modification-time-annotation-in-org-roam-node-find-minad][minad/marginalia modification time]] and [[https://github.com/org-roam/org-roam/wiki/User-contributed-Tricks#showing-the-number-of-backlinks-for-each-node-in-org-roam-node-find][backlinks count]].
#+begin_src emacs-lisp

  (with-eval-after-load 'org-roam

    (cl-defmethod org-roam-node-backlinkscount ((node org-roam-node))
      (let* ((count (caar (org-roam-db-query
                           [:select (funcall count source)
                                    :from links
                                    :where (= dest $s1)
                                    :and (= type "id")]
                           (org-roam-node-id node)))))
        (format "[%d]" count)))

    (setq org-roam-node-display-template
          (concat "${title:80}" (propertize "${tags:20}" 'face 'org-tag))
          org-roam-node-annotation-function
          (lambda (node)
            (concat (org-roam-node-backlinkscount node) " "
                    (marginalia--time (org-roam-node-file-mtime node)))))
    )

#+end_src

** Prettify Symbols
#+begin_src emacs-lisp

  (setq-default prettify-symbols-alist
                '(("#+begin_src emacs-lisp" . "")
                  ("#+begin_src nix" . "")
                  (":ATTACH:" . "🔗")
                  ("#+begin_src" . "»")
                  ("#+end_src" . "«")
                  ("#+begin_quote" . "")
                  ("#+end_quote" . "")
                  ("#+RESULTS:" . "󰥤")
                  (":tangle" . "󰯊")
                  (":mkdirp yes" . "")
                  ("lambda" . "λ")
                  ("(interactive)" . "")))

  (setq prettify-symbols-unprettify-at-point 'right-edge)

#+end_src

** Settings
Making org mode more akin to my linking.
#+begin_src emacs-lisp

  ;; Using RETURN to follow links in Org/Evil
  ;; Unmap keys in 'evil-maps if not done, (setq org-return-follows-link t) will not work
  (with-eval-after-load 'evil-maps
    (define-key evil-motion-state-map (kbd "SPC") nil)
    (define-key evil-motion-state-map (kbd "RET") nil)
    (define-key evil-motion-state-map (kbd "TAB") nil))

  ;; Setting RETURN key in org-mode to follow links
  (setq org-return-follows-link  t)

  (use-package org
    :ensure nil
    :defer t
    :hook
    (org-mode . org-indent-mode)
    (org-mode . prettify-symbols-mode)
    (org-mode . (lambda () (display-line-numbers-mode -1)))
    (org-mode . visual-line-mode)
    (org-mode . variable-pitch-mode)
    :custom
    (org-attach-id-dir "attachments/")
    (org-attach-use-inheritance t)
    (org-attach-method 'mv)
    (org-startup-with-inline-images t)
    (org-image-align 'center)
    (org-fontify-quote-and-verse-blocks t)
    (org-support-shift-select t)
    (org-hide-emphasis-markers t)
    :config
    (require 'docx-to-org)
    (nox/org-font-setup))

#+end_src

** Small Utilities
Add a table of content to org mode files. For this you just need to add a level one heading with :toc: tag.
#+begin_src emacs-lisp

  (use-package toc-org
      :hook (org-mode . toc-org-enable))

#+end_src

Make editing of links and any other hidden emphasis markers easy.
#+begin_src emacs-lisp

  (use-package org-appear
    :hook (org-mode . org-appear-mode)
    :custom (org-appear-autolinks t))

#+end_src

Enable auto tangling of org files. For this to work you need to set a new option to the file namely ~#+autotangle: t~.
#+begin_src emacs-lisp

  (use-package org-auto-tangle
    :hook (org-mode . org-auto-tangle-mode))

#+end_src

For better, faster and easier src blocks and other kinds of blocks.
#+begin_src emacs-lisp

  (with-eval-after-load 'org
    (require 'org-tempo)
    (add-to-list 'org-structure-template-alist '("sh" . "src shell"))
    (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
    (add-to-list 'org-structure-template-alist '("go" . "src go"))
    (add-to-list 'org-structure-template-alist '("nix" . "src nix"))
    (add-to-list 'org-structure-template-alist '("py" . "src python"))

    ;; turn off < auto pairing inside org-mode
    (add-hook 'org-mode-hook (lambda ()
                               (setq-local electric-pair-inhibit-predicate
                                           `(lambda (c)
                                              (if (char-equal c ?<) t (,electric-pair-inhibit-predicate c)))))))

#+end_src

** SVG Tags
#+begin_src emacs-lisp

  (use-package svg-tag-mode
    :after org
    :hook
    (org-mode . svg-tag-mode)
    :custom
    (svg-tag-action-at-point 'edit)
    :config
    (set-face-attribute 'svg-tag-default-face nil :inherit 'fixed-pitch)
    (defconst date-re "[0-9]\\{4\\}-[0-9]\\{2\\}-[0-9]\\{2\\}")
    (defconst time-re "[0-9]\\{2\\}:[0-9]\\{2\\}")
    (defconst day-re "[A-Za-z]\\{3\\}")
    (defconst day-time-re (format "\\(%s\\)? ?\\(%s\\)?" day-re time-re))
    (setq svg-tag-tags
          `(
            ;; Task priority
            ("\\[#A\\]" .
             ((lambda (tag) (svg-tag-make "#A"  :face
                                          ;;`(:foreground ,(catppuccin-get-color 'red))
                                          :beg 2 :end -1 :margin 0))))
            ("\\[#B\\]" .
             ((lambda (tag) (svg-tag-make "#B"  :face
                                          ;;`(:foreground ,(catppuccin-get-color 'yellow))
                                          :beg 2 :end -1 :margin 0))))
            ("\\[#C\\]" .
             ((lambda (tag) (svg-tag-make "#C"  :face
                                          ;;`(:foreground ,(catppuccin-get-color 'green))
                                          :beg 2 :end -1 :margin 0))))

            ;; Citation of the form [cite:@Knuth:1984]
            ("\\(\\[cite:@[A-Za-z]+:\\)" .
             ((lambda (tag) (svg-tag-make tag  :inverse t
                                          :beg 7 :end -1
                                          :crop-right t))))
            ("\\[cite:@[A-Za-z]+:\\([0-9]+\\]\\)" .
             ((lambda (tag) (svg-tag-make tag  :end -1
                                          :crop-left t))))

            ;; Active date (with or without day name, with or without time)
            (,(format "\\(<%s>\\)" date-re) .
             ((lambda (tag) (svg-tag-make tag  :beg 1
                                          :end -1
                                          :margin 0))))
            (,(format "\\(<%s \\)%s>" date-re day-time-re) .
             ((lambda (tag) (svg-tag-make tag  :beg 1
                                          :inverse nil
                                          :crop-right t
                                          :margin 0))))
            (,(format "<%s \\(%s>\\)" date-re day-time-re) .
             ((lambda (tag) (svg-tag-make tag  :end -1
                                          :inverse t
                                          :crop-left t
                                          :margin 0))))

            ;; Inactive date  (with or without day name, with or without time)
            (,(format "\\(\\[%s\\]\\)" date-re) .
             ((lambda (tag) (svg-tag-make tag  :beg 1
                                          :end -1
                                          :margin 0
                                          :face 'org-date))))
            (,(format "\\(\\[%s \\)%s\\]" date-re day-time-re) .
             ((lambda (tag) (svg-tag-make tag  :beg 1
                                          :inverse nil
                                          :crop-right t
                                          :margin 0
                                          :face 'org-date))))
            (,(format "\\[%s \\(%s\\]\\)" date-re day-time-re) .
             ((lambda (tag) (svg-tag-make tag  :end -1
                                          :inverse t
                                          :crop-left t
                                          :margin 0
                                          :face 'org-date))))

            ;; Todos
            ("TODO" .
             ((lambda (tag) (svg-tag-make "TODO"
                                          :face
                                          ;;`(:foreground ,(catppuccin-get-color 'peach))
                                          :inverse t
                                          :margin 0))))
            ("READ" .
             ((lambda (tag) (svg-tag-make "READ"
                                          :face
                                          ;;`(:foreground ,(catppuccin-get-color 'rosewater))
                                          :inverse t
                                          :margin 0))))
            ("PLAN" .
             ((lambda (tag) (svg-tag-make "PLAN"
                                          :face
                                          ;;`(:foreground ,(catppuccin-get-color 'maroon))
                                          :inverse t
                                          :margin 0))))
            ("PROG" .
             ((lambda (tag) (svg-tag-make "PROG"
                                          :face
                                          ;;`(:foreground ,(catppuccin-get-color 'teal))
                                          :inverse t
                                          :margin 0))))
            ("WAIT" .
             ((lambda (tag) (svg-tag-make "WAIT"
                                          :face
                                          ;;`(:foreground ,(catppuccin-get-color 'red))
                                          :inverse t
                                          :margin 0))))
            ("DONE" .
             ((lambda (tag) (svg-tag-make "DONE"
                                          :face
                                          ;;`(:foreground ,(catppuccin-get-color 'green))
                                          :inverse t
                                          :margin 0))))
            ("CANC" .
             ((lambda (tag) (svg-tag-make "CANC"
                                          :face
                                          ;;`(:foreground
                                          ;;  ,(catppuccin-get-color 'overlay0))
                                          :inverse t
                                          :margin 0)))))))

#+end_src

** Visual Fill Column
Centering org documents for easier editing experience.
#+begin_src emacs-lisp

  (use-package visual-fill-column
    :hook
    (org-mode . visual-fill-column-mode)
    (org-agenda-mode . visual-fill-column-mode)
    :custom
    (visual-fill-column-width 100)
    (visual-fill-column-center-text t))

#+end_src


* Shells & Terminals
** Eshell
#+begin_src emacs-lisp

  (setq eshell-rc-script (concat nox/emacs-directory "eshell/profile")
        eshell-aliases-file (concat nox/emacs-directory "eshell/aliases")
        eshell-history-size 5000
        eshell-buffer-maximum-lines 5000
        eshell-hist-ignoredups t
        eshell-scroll-to-bottom-on-input t
        eshell-destroy-buffer-when-process-dies t
        eshell-visual-commands'("bash" "fish" "htop" "ssh" "top" "zsh"))

  (add-hook 'eshell-mode-hook (lambda ()
                                (hide-mode-line-mode)))

  (use-package eshell-toggle
    :commands eshell-toggle
    :custom
    (eshell-toggle-run-command nil)
    (eshell-toggle-size-fraction 2))

#+end_src

#+begin_src eshell-alias :tangle eshell/aliases :mkdirp yes

  alias cls clear 1
  alias e 'find-file $1'

#+end_src

** Vterm
#+begin_src emacs-lisp

  (use-package vterm
    :commands (vterm vterm-toggle)
    :hook
    (vterm-mode . (lambda () (display-line-numbers-mode -1)))
    (vterm-mode . hide-mode-line-mode)
    :custom
    (vterm-shell "zsh -c nu"))

  (use-package vterm-toggle :commands vterm-toggle)

#+end_src

* Undo
** Undo Fu
[[https://github.com/emacsmirror/undo-fu][Undo Fu]] is a light weight wrapper for Emacs built-in undo system, adding convenient undo/redo without losing access to the full undo history, allowing you to visit all previous states of the document if you need. Here we also increase the default undo limit from =0.15mb= to the following mentioned values.
#+begin_src emacs-lisp

  (use-package undo-fu
    :after evil
    :config
    (setq undo-limit 67108864)          ; 64mb.
    (setq undo-strong-limit 100663296)  ; 96mb.
    (setq undo-outer-limit 1006632960)) ; 960mb.

#+end_src

We also use [[https://codeberg.org/ideasman42/emacs-undo-fu-session][undo fu session]] to persist the undo/redo history through emacs sessions. This means the undo history won't be lost when restarting emacs.
#+begin_src emacs-lisp

  (use-package undo-fu-session
    :after undo-fu
    :config
    (undo-fu-session-global-mode)
    (setq undo-fu-session-incompatible-files
          '("/COMMIT_EDITMSG\\'" "/git-rebase-todo\\'")))

#+end_src

** Vundo
Alongside [[#undo-fu][undo fu]] we also use [[https://github.com/casouri/vundo][vundo]] to show the undo history in a tree graph. This makes moving around the undo history super easy. Here we improve the default vundo to use unicode symbols, better colors as well as a easy keybind to get inside vundo.
#+begin_src emacs-lisp

  (use-package vundo
    :commands vundo
    :custom
    (vundo-glyph-alist vundo-unicode-symbols)
    :config
    ;; Take less on-screen space.
    (setq vundo-compact-display t))

  (with-eval-after-load 'evil (evil-define-key 'normal 'global (kbd "C-M-u") 'vundo))

#+end_src

;; Local Variables:
;; eval: (rainbow-delimiters-mode)
;; End:
